plugins {
  id 'java-library'
  id 'groovy'
  id 'org.openapi.generator' version '4.2.2'
  id 'com.adarshr.test-logger' version '1.6.0'
  id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  compile 'io.swagger:swagger-annotations:1.5.24'
  compile 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'javax.annotation:javax.annotation-api:1.3.2'

  implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'

  implementation 'com.squareup.retrofit2:retrofit:2.7.0'
  implementation 'com.squareup.retrofit2:converter-jackson:2.7.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
  implementation 'com.squareup.retrofit2:converter-scalars:2.7.0'
  implementation 'io.gsonfire:gson-fire:1.8.3'

  // TEST
  testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
  testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

openApiGenerate {
  generatorName = "java"
  library = "retrofit2"
  inputSpec = "$rootDir/src/main/resources/psd2-api 1.3.4 20190717v1.yaml".toString()
  outputDir = "$buildDir/generated".toString()
  apiPackage = "it.tccr.fabrick.api"
  invokerPackage = "it.tccr.fabrick.api.invoker"
  modelPackage = "it.tccr.fabrick.api.model"
  modelNameSuffix = "Dto"
  configOptions = [
    dateLibrary: "java8"
  ]
  skipValidateSpec = true
  generateModelTests = false
  generateModelDocumentation = false
  generateApiTests = false
  generateApiDocumentation = false
  withXml = false
}

sourceSets {
  main {
    java.srcDirs += "$buildDir/generated/src/main/java"
  }
}

compileJava.dependsOn tasks.openApiGenerate
